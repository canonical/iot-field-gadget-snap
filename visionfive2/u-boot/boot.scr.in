# mmc/scsi etc & number
setenv devtype  mmc
setenv devnum   1
# The ubuntu-{seed,boot} partitions
setenv seedpart 3
setenv bootpart 4

# Address to load the FIT Image to
setenv kernel_name kernel.img
setenv ramdisk_name initrd.img
setenv fdt_name jh7110-starfive-visionfive-2-v1.2a.dtb 
setenv kernel_addr_r 0x50000000

echo "Loading ubuntu-seed configuration..."
load ${devtype} ${devnum}:${seedpart} ${scriptaddr} uboot/ubuntu/boot.sel
env import ${scriptaddr}

echo "Snapd boot mode: ${snapd_recovery_mode}"

if test "${snapd_recovery_mode}" = "run"
then
    echo "Loading ubuntu-boot configuration..."
    load ${devtype} ${devnum}:${bootpart} ${scriptaddr} uboot/ubuntu/boot.sel
    env import ${scriptaddr}
    setenv kernel_bootpart "${bootpart}"
    if test "${kernel_status}" = "try"
    then
        echo "Trying to boot with ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_try_kernel}/"
    else
        echo "Using ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_kernel}/"
    fi
else
    # Prefix within the seed partition to the boot files
    setenv prefix "systems/${snapd_recovery_system}/kernel/"
        setenv kernel_bootpart "${seedpart}"
fi

setenv console  "earlyprintk console=tty1 console=ttyS0,115200 earlycon=sbi"
#setenv console  "earlyprintk console=ttyS0,115200 earlycon=sbi"
setenv console "earlycon=sbi"
setenv bootargs "${bootargs} ${console} rootwait rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system}"

echo "Loading ${prefix}${kernel_name}..."

load ${devtype} ${devnum}:${kernel_bootpart} ${fdt_addr_r} "${prefix}dtbs/${fdt_name}"
load ${devtype} ${devnum}:${kernel_bootpart} ${kernel_addr_r} "${prefix}${kernel_name}"
load ${devtype} ${devnum}:${kernel_bootpart} ${ramdisk_addr_r} "${prefix}${ramdisk_name}"

booti ${kernel_addr_r} ${ramdisk_addr_r}:${filesize} ${fdt_addr_r}
#bootm ${kernel_addr_r}
