## Note that a lot of environment variables are already defined!
## Specifically, kernel_addr_r, ramdisk_addr_r, & fdtcontroladdr
## Our device tree is also already preloaded by u-boot.toc1

# mmc/scsi etc & number
setenv devtype  mmc
setenv devnum   0
# The ubuntu-{seed,boot} partitions
setenv seedpart 1
setenv bootpart 2

# Address to load the config & ramdisk to
setenv config_addr_r  0x41900000
setenv ramdisk_addr_r 0x50000000

# Kernel, initrd, & fdt filename
setenv ramdisk_name initrd.img
setenv kernel_name  kernel.img

echo "Loading ubuntu-seed configuration..."
load ${devtype} ${devnum}:${seedpart} ${config_addr_r} uboot/ubuntu/boot.sel
env import ${config_addr_r}

echo "Snapd boot mode: ${snapd_recovery_mode}"

if test "${snapd_recovery_mode}" = "run"
then
    echo "Loading ubuntu-boot configuration..."
    load ${devtype} ${devnum}:${bootpart} ${config_addr_r} uboot/ubuntu/boot.sel
    env import ${config_addr_r}
    setenv kernel_bootpart "${bootpart}"
    if test "${kernel_status}" = "try"
    then
        echo "Trying to boot with ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_try_kernel}/"
    else
        echo "Using ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_kernel}/"
    fi
else
    setenv prefix "systems/${snapd_recovery_system}/kernel/"
    setenv kernel_bootpart "${seedpart}"
fi

setenv console "earlycon=sbi console=ttyS0,115200n8"
setenv bootargs "${console} rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system}"

echo "Loading kernel from ${devtype} ${devnum}:${kernel_bootpart} to address ${kernel_addr_r}"
load ${devtype} ${devnum}:${kernel_bootpart} ${kernel_addr_r} "${prefix}${kernel_name}"

echo "Loading initrd from ${devtype} ${devnum}:${kernel_bootpart} to address ${ramdisk_addr_r}"
load ${devtype} ${devnum}:${kernel_bootpart} ${ramdisk_addr_r} "${prefix}${ramdisk_name}"
setenv ramdisk_size "${filesize}"

echo "Booting kernel with bootargs as ${bootargs}"
booti ${kernel_addr_r} ${ramdisk_addr_r}:${ramdisk_size} ${fdtcontroladdr}
