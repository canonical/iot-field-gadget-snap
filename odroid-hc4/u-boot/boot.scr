setenv devtype mmc
setenv devnum 1
setenv seedpart 1
setenv bootpart 2

setenv kernel_name    "kernel.img"
setenv ramdisk_name   "initrd.img"
setenv fdt_name       "dtbs/meson-sm1-odroid-hc4.dtb"

setenv kernel_addr_r  "0x20000000"
setenv ramdisk_addr_r "0x28000000"
setenv fdt_addr_r     "0x10000000"
setenv scriptaddr     "0x18000000"

echo "Loading ubuntu-seed configuration..."
load ${devtype} ${devnum}:${seedpart} ${scriptaddr} uboot/ubuntu/boot.sel
env import ${scriptaddr}

echo "Snapd boot mode: ${snapd_recovery_mode}"

if test "${snapd_recovery_mode}" = "run"
then
    echo "Loading ubuntu-boot configuration..."
    load ${devtype} ${devnum}:${bootpart} ${scriptaddr} uboot/ubuntu/boot.sel
    env import ${scriptaddr}
    setenv kernel_bootpart "${bootpart}"
    if test "${kernel_status}" = "try"
    then
        echo "Trying to boot with ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_try_kernel}/"
    else
        echo "Using ${snap_kernel}"
        setenv prefix "uboot/ubuntu/${snap_kernel}/"
    fi
else
    # Prefix within the seed partiion to the boot files
    setenv prefix "systems/${snapd_recovery_system}/kernel/"
    setenv kernel_bootpart "${seedpart}"
fi

setenv bootargs "earlycon rw snapd_recovery_mode=${snapd_recovery_mode} snapd_recovery_system=${snapd_recovery_system} ${bootargs}"

echo "Loading ${prefix}${kernel_name}..."
load ${devtype} ${devnum}:${kernel_bootpart} ${kernel_addr_r} "${prefix}${kernel_name}"
echo "Loading ${prefix}${ramdisk_name}..."
load ${devtype} ${devnum}:${kernel_bootpart} ${ramdisk_addr_r} "${prefix}${ramdisk_name}"
setenv ramdisk_size $filesize
echo "Loading ${prefix}${fdt_name}..."
load ${devtype} ${devnum}:${kernel_bootpart} ${fdt_addr_r} "${prefix}${fdt_name}"

fdt addr ${fdt_addr_r}

booti ${kernel_addr_r} ${ramdisk_addr_r}:${ramdisk_size} ${fdt_addr_r}
